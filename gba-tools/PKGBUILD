# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=gba-tools
pkgver=1.2.0
pkgrel=1
pkgdesc='gba tools'
arch=('any')
url="https://github.com/devkitpro/${pkgname}"
license=('GPL')
options=(!strip libtool staticlibs)
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('516e5037e48b3dbaaa41291f83146d039b35b04ccc6887a7e92858ff59ea6daa')
groups=('gba-dev')

if [[ "$CHOST" == *"-w64-mingw32" ]]; then
  depends=('tool-dlls')
fi

build() {

  cd $pkgname-$pkgver

  source ~/cross-build-$CHOST.sh

  ./configure --host=$CHOST --prefix=/opt/devkitpro/tools

  make

}

package() {

  cd $pkgname-$pkgver

  source ~/cross-build-$CHOST.sh

  make DESTDIR="$pkgdir" install-strip

  install -Dm644 COPYING "$pkgdir"/opt/devkitpro/licenses/$pkgname/COPYING
  
}
